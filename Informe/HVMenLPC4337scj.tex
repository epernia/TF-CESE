\section{Port de HVM SCJ al microcontrolador NXPLPC4337} \label{sec:HVMscjEnLPC4337}

En esta etapa final del \textit{port} se definen las funciones nativas de la HAL de HVM, necesarias para poder realizar programas SCJ.

%--------------------------------------------------
\bigskip
\titulo{Archivo \textbf{LPC4337\_natives\_SCJ.c}}

Estas funciones utilizan las provistas en el archivo \textbf{sapi\_Tick.c} de la biblioteca de C.

\begin{itemize}
\item
\textbf{void start\_system\_tick(void)}, \textbf{void stop\_system\_tick(void)}.  Estas funciones inician utilizan la función \textbf{void tickConfig(uint64\_t tickRateHz)}, la misma recibe un parámetro que utiliza para configurar el periférico \textbf{SysTick}, si el mismo es 0, frena la interrupción de \textit{SysTick}. 
\item
\textbf{int16 n\_vm\_RealtimeClock\_awaitNextTick(int32 *sp)}. Esta función queda en un bucle hasta que cambie el valor de la variable global \textbf{systemTick}. 
\item
\textbf{int16 n\_vm\_RealtimeClock\_getNativeResolution(int32 *sp)}. La función \textbf{void tickConfig(uint32\_t tickRateHz)} almacena el valor \textbf{tickRateHz} recibido como parámetro en un variable global, este valor es utilizado para calcular el tiempo entre dos \textit{ticks}.
\item
\textbf{int16 n\_vm\_RealtimeClock\_getNativeTime(int32 *sp)}. Devuelve el tiempo actual del reloj de tiempo real como un objeto AbsoluteTime con mili segundos y nano segundos.
\end{itemize}


%--------------------------------------------------
\bigskip
\titulo{Archivo \textbf{LPC4337\_interrupt.s}}

Para la realización de estas funciones en \textit{assembler} se necesita conocer en detalle la arquitectura del microcontrolador. Las plataformas EDU-CIAA-NXP y CIAA-NXP contienen un LPC4337. Este microcontrolador ARM posee dos núcleos (como se adelanta en la sección [\ref{sec:proyectoCIAA}]) un núcleo Cortex-M0 y un Cortex-M4. Este \textit{port} de HVM utiliza únicamente le núcleo Cortex-M4.

\medskip

El documento \textit{ARM Architecture Procedure Call Standard}, (AAPCS) especifica como debe comportarse un programa durante llamadas a procedimientos. Esta información es necesaria poder mezclar funciones en lenguaje C con funciones en \textit{assembler}. En [\ref{bib:YiuDefinitiveGuide}] se indica que para el Cortex-M4 los pasajes de parámetros se realizan mediante los registros \textbf{r0} a \textbf{r3}, mientras que el valor de retorno se envía por el registro \textbf{r0}. 

\medskip

Se muestra a continuación la implementación de las tres funciones en \textit{assembler} que se utilizan para el cambio de contexto.

\medskip

La función \textbf{\_yield}. Debe guardar todos los registros en la pila, guardar el puntero a pila en la variable global \textbf{stackPointer} y llama a la función \textbf{transfer}. Cuando termina la ejecución de \textbf{transfer} debe guardar el valor de la variable global \textbf{stackPointer} al puntero a pila, restaurar todos los registros y retornar.

\begin{lstlisting}[language=C, numbers=left]
.syntax unified

/* --------------[ _yield ]-------------- */
	.text
	.thumb_func
	.align	2
	.word	stackPointer
	.global	_yield
_yield:

	/* Guarda el msp en r0 */
	mrs r0,msp

	/* Guarda el contexto de FPU */
	tst lr,0x10
	it eq
	vstmdbeq r0!,{s16-s31}

	/* Guarda el contexto, enteros */
	stmdb r0!,{r4-r11,lr}

	/* Guarda el stack pointer del proceso actual en r0 */
	ldr r1,=stackPointer
	str r0,[r1]

	/* Ejecuta una funcion externa que realiza el cambio de proceso */
	bl	_transfer

	/* Carga el stack pointer del nuevo proceso en r0 */
	ldr r1,=stackPointer
	ldr r0,[r1]

	/* Recupera el contexto, enteros */
	ldmia r0!,{r4-r11,lr}

	/* Recupera contexto FPU si es necesario */
	tst lr,0x10
	it eq
	vldmiaeq r0!,{s16-s31}

	/* Guarda r0 en el msp */
	msr msp,r0
   
	bx lr
\end{lstlisting}


\medskip
   
Cuando se llama a la función \textbf{pointer* get\_stack\_pointer(void)} debe retornar el valor del puntero a pila. 

\begin{lstlisting}[language=C, numbers=left]
/* --------------[ get_stack_pointer ]-------------- */
	.text
	.thumb_func
	.align	2
	.global	get_stack_pointer
get_stack_pointer:

	/* Guarda el msp en r0 */
	mrs r0,msp

	bx lr
\end{lstlisting}

La función \textbf{set\_stack\_pointer(void)} Debe establecer el valor de la variable global \textbf{stackPointer} en el puntero a pila y retornar a la función invocante.

\begin{lstlisting}[language=C, numbers=left]
/* --------------[ set_stack_pointer ]-------------- */
	.text
	.thumb_func
	.align	2
	.global	set_stack_pointer
set_stack_pointer:

	/* Carga el stack pointer del nuevo proceso en r0 */
	ldr r1,=stackPointer
	ldr r0,[r1]
   
	/* Guarda r0 en el msp */
	msr msp,r0

	bx lr
\end{lstlisting}

%--------------------------------------------------

Para poder compilar una aplicación SCJ se debe agregar este archivo al \textit{Makefile} desarrollado en la sección [\ref{sec:HVMenLPC4337}].